/*******************************************************************************
 * The MIT License (MIT)
 *
 * Copyright (c) 2013-2016 Chaosdorf e.V.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 ******************************************************************************/

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
        classpath 'gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: "com.github.spotbugs"

dependencies {
    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    implementation 'org.jetbrains:annotations:23.0.0'
    // Retrofit >= 2.7 needs Android 5+
    implementation 'com.squareup.retrofit2:retrofit:2.6.4'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.4'
    implementation 'com.google.zxing:android-integration:3.3.0'
    implementation 'com.shamanland:fab:0.0.8'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation "com.artit-k:license-fragment:1.3.0"
    implementation 'com.github.YtvwlD:slycalendarview:dev-SNAPSHOT'
}

//see https://stackoverflow.com/a/22183825/2192464
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
        options.compilerArgs << "-Xlint:unchecked"
    }
}

android {
    compileSdkVersion 31
    signingConfigs {}
    lintOptions {
      abortOnError false
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    defaultConfig {
        targetSdkVersion 31
        minSdkVersion 16
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}


// see https://stackoverflow.com/a/52718914/2192464
sourceSets {
    main {
        java.srcDirs = []
    }
}
tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    dependsOn 'assembleDebug'
    classes = files("$projectDir.absolutePath/build/intermediates/javac/debug/classes/")
    sourceDirs = fileTree('src/main/java')
}
spotbugs {
    toolVersion = "4.2.2"
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
}
